plugins {
    id 'java'
}

group = project.bot_group
version = project.bot_version

repositories {
    mavenCentral()
}

dependencies {
    implementation("io.github.cdimascio:dotenv-java:${project.dotenv_java_version}")
    implementation("com.google.guava:guava:${project.guava_version}")
    implementation("ch.qos.logback:logback-classic:${project.logback_version}")
    implementation("de.exlll:configlib-yaml:${project.configlib_verison}")
    implementation("net.dv8tion:JDA:${project.jda_version}") {
        exclude module: 'opus-java' // Disables audio natives
    }
}

tasks.register('setupRunDir', Copy) {
    if(!file("$projectDir/run/.env").exists()) {
        from("$projectDir/.env.example")
        into("$projectDir/run/")
        rename { ".env" }
    }
}

tasks.register('runChonky', JavaExec) {
    dependsOn('setupRunDir')
    group = 'application'
    description = 'Runs the Chonky Chicken Discord bot'
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'com.mrcrayfish.chonky_bot.Main'
    workingDir = file("$projectDir/run")
}